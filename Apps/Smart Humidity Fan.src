/**
 *  Smart Humidity Fan
 *
 *  Turns on a fan when you start taking a shower... turns it back off when you are done.
 *    -Uses humidity change rate for rapid response
 *    -Timeout option when manaully controled (for stench mitigation)
 *
 *  Copyright 2018 Craig Romei
 *  GNU General Public License v2 (https://www.gnu.org/licenses/gpl-2.0.txt)
 *
 */

definition(
    name: "Smart Humidity Fan",
    namespace: "Craig.Romei",
    author: "Craig Romei",
    description: "Control a fan (switch) based on relative humidity.",
    category: "Convenience",
    iconUrl: "https://raw.githubusercontent.com/napalmcsr/SmartThingsStuff/master/smartapps/craig-romei/Bathroom_Fan.jpg",
    iconX2Url: "https://raw.githubusercontent.com/napalmcsr/SmartThingsStuff/master/smartapps/craig-romei/Bathroom_Fan.jpg",
    iconX3Url: "https://raw.githubusercontent.com/napalmcsr/SmartThingsStuff/master/smartapps/craig-romei/Bathroom_Fan.jpg"
)

preferences
{
	section("Bathroom Devices")
    {
    	paragraph "NOTE: The humidity sensor you select will need to report about 5 min or less."
		input "HumiditySensor", "capability.relativeHumidityMeasurement", title: "Humidity Sensor:", required: true
		input "FanSwitch", "capability.switch", title: "Fan Location:", required: true
    }
    section("Fan Activation")
    {
		input "HumidityIncreaseRate", "number", title: "Humidity Increase Rate :", required: true, defaultValue: 2
        input "HumidityThreshold", "number", title: "Humidity Threshold (%):", required: false, defaultValue: 65
	}
    section("Fan Deactivation")
    {
		input "HumidityDropTimeout", "number", title: "How long after the humidity starts to drop should the fan turn off (minutes):", required: true, defaultValue:  10
		input "HumidityDropLimit", "number", title: "How much should the humidty drop between readings before triggering the turn off delay:", required: true, defaultValue:  1
	}
    section("Manual Activation")
    {
    	paragraph "When should the fan turn off when turned on manually?"
        input "ManualControlMode", "enum", title: "Off After Manual-On?", required: true, options: ["Manually", "By Humidity", "After Set Time"], defaultValue: "After Set Time"
        paragraph "How many minutes until the fan is auto-turned-off?"
        input "ManualOffMinutes", "number", title: "Auto Turn Off Time (minutes)?", required: false, defaultValue: 20
    }
}

def installed()
{
    initialize()
}

def updated()
{
	unsubscribe()
    initialize()
}

def initialize()
{
    log.info "info"
    log.debug "debug"
	state.humpres = false
	state.OverThreshold = false
	state.AutomaticallyTurnedOn = false
	state.TurnOffLaterStarted = false
    subscribe(HumiditySensor, "humidity", HumidityHandler)
    subscribe(FanSwitch, "switch", FanSwitchHandler)
}

def HumidityHandler(evt)
{
    log.info "HumidityHandler:running humidity check"
	state.OverThreshold = CheckThreshold(evt)
	state.humpres = GetHumidityChange(evt)
	log.info "HumidityHandler: humiditychange = ${state.humpres}"
	log.info "HumidityHandler: state.OverThreshold = ${state.OverThreshold}"
	log.info "HumidityHandler: state.AutomaticallyTurnedOn = ${state.AutomaticallyTurnedOn}"
	log.info "HumidityHandler: state.TurnOffLaterStarted = ${state.TurnOffLaterStarted}"
	log.info "HumidityHandler: state.HumidityChangeRate = ${state.HumidityChangeRate}"
	log.info "HumidityHandler: FanSwitch.currentValue = ${FanSwitch.currentValue("switch")}"
    
	//if the humidity is high (or rising fast) and the fan is off, kick on the fan
    if (((state.HumidityChangeRate>HumidityIncreaseRate)||state.OverThreshold) && FanSwitch.currentValue("switch") == "off")
    {
		state.AutomaticallyTurnedOn = true
		state.TurnOffLaterStarted = false
        state.AutomaticallyTurnedOnAt = now()
        log.debug "HumidityHandler:Fan On"
       	FanSwitch.on()
    }
    //turn off the fan when humidity returns to normal and it was kicked on by the humidity sensor
    else if((state.AutomaticallyTurnedOn || ManualControlMode == "By Humidity")&& !state.TurnOffLaterStarted)
    {
        if(-state.HumidityChangeRate>=HumidityDropLimit)
        {
            if(HumidityDropTimeout == 0)
            {
        		log.debug "HumidityHandler:Fan Off"
                        TurnOffFanSwitch()
            }
            else
            {
				log.debug "HumidityHandler:Turn Fan off in ${HumidityDropTimeout} minutes."
				state.TurnOffLaterStarted = true
				runIn(60 * HumidityDropTimeout.toInteger(), TurnOffFanSwitchCheckHumidity)
				log.info "HumidityHandler: state.TurnOffLaterStarted = ${state.TurnOffLaterStarted}"
            }
        }
	}
}

def FanSwitchHandler(evt)
{
    log.info "FanSwitchHandler::Switch changed"
	log.info "FanSwitchHandler: ManualControlMode = ${ManualControlMode}"
	log.info "FanSwitchHandler: ManualOffMinutes = ${ManualOffMinutes}"
	log.info "HumidityHandler: state.AutomaticallyTurnedOn = ${state.AutomaticallyTurnedOn}"
	switch(evt.value)
    {
    	case "on":
                if(!state.AutomaticallyTurnedOn && (ManualControlMode == "After Set Time") && ManualOffMinutes)
                {
                    if(ManualOffMinutes == 0)
                    {
        				log.debug "FanSwitchHandler::Fan Off"
                        TurnOffFanSwitch()
                    }
                    else
                    {
						log.info "FanSwitchHandler::Will turn off later"
                        runIn(60 * ManualOffMinutes.toInteger(), TurnOffFanSwitch)
                    }
                }
	        break
        case "off":
    		log.info "FanSwitchHandler::Switch turned off"
		    state.AutomaticallyTurnedOn = false
			state.TurnOffLaterStarted = false
        	break
    }
}

def TurnOffFanSwitchCheckHumidity()
{
    log.debug "TurnOffFanSwitchCheckHumidity: Function Start"
	if(FanSwitch.currentValue("switch") == "on")
    {
        log.debug "TurnOffFanSwitchCheckHumidity: state.HumidityChangeRate ${state.HumidityChangeRate}"
        if(state.HumidityChangeRate > 0)
        {
        	log.debug "TurnOffFanSwitchCheckHumidity: Didn't turn off fan because humidity rate is ${state.HumidityChangeRate}"
            state.AutomaticallyTurnedOn = true
            state.AutomaticallyTurnedOnAt = now()
            runIn(600, TurnOffFanSwitchCheckHumidity)
        }
        else
        {
        	log.debug "TurnOffFanSwitchCheckHumidity: Turning the Fan off now"
        	TurnOffFanSwitch()
        }
    }
}

def TurnOffFanSwitch()
{
    if(FanSwitch.currentValue("switch") == "on")
    {
        log.debug "TurnOffFanSwitch:Fan Off"
        FanSwitch.off()
	state.AutomaticallyTurnedOn = false
	state.TurnOffLaterStarted = false
    }
}

def CheckThreshold(evt)
{
	double lastevtvalue = Double.parseDouble(evt.value.replace("%", ""))
	if(lastevtvalue >= HumidityThreshold)
		{
			
				log.debug "IsHumidityPresent: Humidity is above the Threashold"
			return true
		}
		else
		{
			return false
		}
}
def GetHumidityChange(evt)
{
    
    def states = evt.device.eventsSince(new Date((long)(evt.date.getTime() - (21*60000)))).findAll{it.name == "humidity"}
                log.debug "IsHumidityPresent: numStates is ${states.size().toString()}"
    
    double lastevtvalue = Double.parseDouble(evt.value.replace("%", ""))
    def lastevtdate = evt.date
    
	log.debug "IsHumidityPresent: Humidity events in the last 20 min."
	for(int i = 0; i < states.size(); i++)
	{
		log.debug "IsHumidityPresent: i:${i}  humidity: ${states[i].value} Date: ${states[i].date}"
	}
	if(states.size()<2)
	{
		state.HumidityChangeRate =-100
		log.debug "IsHumidityPresent: Not enough events in the last 20 min."
		return false
	}
    else
    {
        state.HumidityChangeRate = 0
        def currentHumidity = Double.parseDouble(states[0].value.replace("%", ""))
        def lastHumidity = Double.parseDouble(states[1].value.replace("%", ""))
        state.HumidityChangeRate = currentHumidity - lastHumidity
        return true
    }

}
